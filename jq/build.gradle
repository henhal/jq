apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 3
        targetSdkVersion 23
        versionCode 22
        versionName "1.1.11"
    }
    buildTypes {
    }
}

version = android.defaultConfig.versionName

def siteUrl = 'https://github.com/code77se/jq'
def gitUrl = 'https://github.com/code77se/jq.git'
group = 'se.code77.jq'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.overview = 'src/main/overview.html'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task debugJar(type: Jar) {
    from 'build/intermediates/classes/debug/'
    archiveName "jq-$version-debug.jar"
}

task releaseJar(type: Jar) {
    from 'build/intermediates/classes/release/'
    archiveName "jq-$version-release.jar"
}

task jar(dependsOn: [debugJar, releaseJar])

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "jq"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
